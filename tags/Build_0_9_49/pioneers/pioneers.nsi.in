; Script generated with the aid of the HM NIS Edit Script Wizard.
; Adapted for Pioneers by Roland Clobus <rclobus@bigfoot.com>
;
; GTK+ Runtime version check as taken from Gaim [2006-01-21]:
;   Original Author: Herman Bloggs <hermanator12002@yahoo.com>
;   Updated By: Daniel Atallah <daniel_atallah@yahoo.com>

;Global Variables
Var GTK_FOLDER

!define GTK_VERSION "2.6"
!define GTK_REG_KEY "SOFTWARE\GTK\2.0"

!define GTK_VERSION_TEXT "You need to install the Gtk+ runtime first. (Minimum version: ${GTK_VERSION}) The runtime can be downloaded from http://gimp-win.sourceforge.net/stable.html"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Pioneers"
!define PRODUCT_VERSION "@VERSION@"
!define PRODUCT_WEB_SITE "http://pio.sf.net"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\pioneers.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

SetCompressor bzip2

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Check whether the Gtk+ runtime is installed
!define MUI_PAGE_CUSTOMFUNCTION_PRE		preWelcomePage
; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "COPYING"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Pioneers"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\pioneers.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Dutch"
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "French"
!insertmacro MUI_LANGUAGE "German"
!insertmacro MUI_LANGUAGE "Hungarian"
!insertmacro MUI_LANGUAGE "Spanish"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Pioneers-${PRODUCT_VERSION}-setup.exe"
InstallDir "$PROGRAMFILES\Pioneers"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "!Minimal install" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "..\..\..\local\pioneers.exe"

  SetOverwrite TRY
; Translations
  SetOutPath "$INSTDIR\locale\de\LC_MESSAGES"
  File "..\..\..\local\locale\de\LC_MESSAGES\pioneers.mo"
  SetOutPath "$INSTDIR\locale\es\LC_MESSAGES"
  File "..\..\..\local\locale\es\LC_MESSAGES\pioneers.mo"
  SetOutPath "$INSTDIR\locale\fr\LC_MESSAGES"
  File "..\..\..\local\locale\fr\LC_MESSAGES\pioneers.mo"
  SetOutPath "$INSTDIR\locale\hu\LC_MESSAGES"
  File "..\..\..\local\locale\hu\LC_MESSAGES\pioneers.mo"
  SetOutPath "$INSTDIR\locale\it\LC_MESSAGES"
  File "..\..\..\local\locale\it\LC_MESSAGES\pioneers.mo"
  SetOutPath "$INSTDIR\locale\nl\LC_MESSAGES"
  File "..\..\..\local\locale\nl\LC_MESSAGES\pioneers.mo"
; No man pages for Windows native distribution
;  SetOutPath "$INSTDIR\man\man6"
;  File "..\..\..\local\man\man6\pioneers-meta-server.6"
;  File "..\..\..\local\man\man6\pioneers-server-console.6"
;  File "..\..\..\local\man\man6\pioneers-server-gtk.6"
;  File "..\..\..\local\man\man6\pioneers.6"
;  File "..\..\..\local\man\man6\pioneersai.6"
; Main toolbar icons
  SetOutPath "$INSTDIR\pixmaps\pioneers"
  File "..\..\..\local\pixmaps\pioneers\bridge.png"
  File "..\..\..\local\pixmaps\pioneers\city.png"
  File "..\..\..\local\pixmaps\pioneers\develop.png"
  File "..\..\..\local\pixmaps\pioneers\dice.png"
  File "..\..\..\local\pixmaps\pioneers\finish.png"
  File "..\..\..\local\pixmaps\pioneers\road.png"
  File "..\..\..\local\pixmaps\pioneers\settlement.png"
  File "..\..\..\local\pixmaps\pioneers\ship.png"
  File "..\..\..\local\pixmaps\pioneers\ship_move.png"
  File "..\..\..\local\pixmaps\pioneers\splash.png"
  File "..\..\..\local\pixmaps\pioneers\trade.png"
; Themes
  SetOutPath "$INSTDIR\themes"
  File "..\..\..\local\themes\board.png"
  File "..\..\..\local\themes\cross.png"
  File "..\..\..\local\themes\desert.png"
  File "..\..\..\local\themes\field.png"
  File "..\..\..\local\themes\forest.png"
  File "..\..\..\local\themes\gold.png"
  File "..\..\..\local\themes\hill.png"
  File "..\..\..\local\themes\mountain.png"
  File "..\..\..\local\themes\pasture.png"
  File "..\..\..\local\themes\plain.png"
  File "..\..\..\local\themes\sea.png"
  File "..\..\..\local\themes\tick.png"
  SetOutPath "$INSTDIR\themes\FreeCIV-like"
  File "..\..\..\local\themes\FreeCIV-like\board.png"
  File "..\..\..\local\themes\FreeCIV-like\desert.png"
  File "..\..\..\local\themes\FreeCIV-like\field.png"
  File "..\..\..\local\themes\FreeCIV-like\forest.png"
  File "..\..\..\local\themes\FreeCIV-like\hill.png"
  File "..\..\..\local\themes\FreeCIV-like\mountain.png"
  File "..\..\..\local\themes\FreeCIV-like\pasture.png"
  File "..\..\..\local\themes\FreeCIV-like\sea.png"
  File "..\..\..\local\themes\FreeCIV-like\theme.cfg"
  SetOutPath "$INSTDIR\themes\Iceland"
  File "..\..\..\local\themes\Iceland\desert.png"
  File "..\..\..\local\themes\Iceland\field_grain.png"
  File "..\..\..\local\themes\Iceland\forest_lumber.png"
  File "..\..\..\local\themes\Iceland\gold.png"
  File "..\..\..\local\themes\Iceland\hill_brick.png"
  File "..\..\..\local\themes\Iceland\mountain_ore.png"
  File "..\..\..\local\themes\Iceland\pasture_wool.png"
  File "..\..\..\local\themes\Iceland\theme.cfg"
  SetOutPath "$INSTDIR\themes\Tiny"
  File "..\..\..\local\themes\Tiny\board.png"
  File "..\..\..\local\themes\Tiny\brick-lorindol.png"
  File "..\..\..\local\themes\Tiny\brick-port.png"
  File "..\..\..\local\themes\Tiny\desert-lorindol.png"
  File "..\..\..\local\themes\Tiny\gold-lorindol.png"
  File "..\..\..\local\themes\Tiny\grain-lorindol.png"
  File "..\..\..\local\themes\Tiny\grain-port.png"
  File "..\..\..\local\themes\Tiny\lumber-lorindol.png"
  File "..\..\..\local\themes\Tiny\lumber-port.png"
  File "..\..\..\local\themes\Tiny\ore-lorindol.png"
  File "..\..\..\local\themes\Tiny\ore-port.png"
  File "..\..\..\local\themes\Tiny\sea-lorindol.png"
  File "..\..\..\local\themes\Tiny\theme.cfg"
  File "..\..\..\local\themes\Tiny\wool-lorindol.png"
  File "..\..\..\local\themes\Tiny\wool-port.png"
  SetOutPath "$INSTDIR\themes\Wesnoth-like"
  File "..\..\..\local\themes\Wesnoth-like\board.png"
  File "..\..\..\local\themes\Wesnoth-like\desert.png"
  File "..\..\..\local\themes\Wesnoth-like\field.png"
  File "..\..\..\local\themes\Wesnoth-like\forest.png"
  File "..\..\..\local\themes\Wesnoth-like\gold.png"
  File "..\..\..\local\themes\Wesnoth-like\hill.png"
  File "..\..\..\local\themes\Wesnoth-like\mountain.png"
  File "..\..\..\local\themes\Wesnoth-like\pasture.png"
  File "..\..\..\local\themes\Wesnoth-like\sea.png"
  File "..\..\..\local\themes\Wesnoth-like\theme.cfg"

; Shortcuts
  SetOutPath "$INSTDIR"
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Pioneers.lnk" "$INSTDIR\pioneers.exe"
  CreateShortCut "$DESKTOP\Pioneers.lnk" "$INSTDIR\pioneers.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section /o "Additional programs" SEC02
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "..\..\..\local\pioneersai.exe"
  File "..\..\..\local\pioneers-editor.exe"

  SetOverwrite TRY
  SetOutPath "$INSTDIR\games\pioneers"
; Names for the AI
  File "..\..\..\local\games\pioneers\computer_names"
; Games for the editor (and server)
  File "..\..\..\local\games\pioneers\5-6-player.game"
  File "..\..\..\local\games\pioneers\Another_swimming_pool_in_the_wall.game"
  File "..\..\..\local\games\pioneers\archipel_gold.game"
  File "..\..\..\local\games\pioneers\canyon.game"
  File "..\..\..\local\games\pioneers\coeur.game"
  File "..\..\..\local\games\pioneers\conquest+ports.game"
  File "..\..\..\local\games\pioneers\conquest.game"
  File "..\..\..\local\games\pioneers\crane_island.game"
  File "..\..\..\local\games\pioneers\Cube.game"
  File "..\..\..\local\games\pioneers\default.game"
  File "..\..\..\local\games\pioneers\Evil_square.game"
  File "..\..\..\local\games\pioneers\four-islands.game"
  File "..\..\..\local\games\pioneers\GuerreDe100ans.game"
  File "..\..\..\local\games\pioneers\henjes.game"
  File "..\..\..\local\games\pioneers\iles.game"
  File "..\..\..\local\games\pioneers\lorindol.game"
  File "..\..\..\local\games\pioneers\Mini_another_swimming_pool_in_the_wall.game"
  File "..\..\..\local\games\pioneers\pond.game"
  File "..\..\..\local\games\pioneers\seafarers-gold.game"
  File "..\..\..\local\games\pioneers\seafarers.game"
  File "..\..\..\local\games\pioneers\small.game"
  File "..\..\..\local\games\pioneers\square.game"
  File "..\..\..\local\games\pioneers\star.game"
  File "..\..\..\local\games\pioneers\x.game"
; Shortcuts
  SetOutPath "$INSTDIR"
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Pioneers Editor.lnk" "$INSTDIR\pioneers-editor.exe"
  CreateShortCut "$DESKTOP\Pioneers Editor.lnk" "$INSTDIR\pioneers-editor.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -AdditionalIcons
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\pioneers.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\pioneers.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "Play games of Pioneers"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Install a game editor and a computer player"
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\pioneers-editor.exe"
  Delete "$INSTDIR\pioneersai.exe"
  Delete "$INSTDIR\pioneers.exe"
  Delete "$INSTDIR\themes\tick.png"
  Delete "$INSTDIR\themes\sea.png"
  Delete "$INSTDIR\themes\plain.png"
  Delete "$INSTDIR\themes\pasture.png"
  Delete "$INSTDIR\themes\mountain.png"
  Delete "$INSTDIR\themes\hill.png"
  Delete "$INSTDIR\themes\gold.png"
  Delete "$INSTDIR\themes\forest.png"
  Delete "$INSTDIR\themes\field.png"
  Delete "$INSTDIR\themes\desert.png"
  Delete "$INSTDIR\themes\cross.png"
  Delete "$INSTDIR\themes\board.png"
  Delete "$INSTDIR\themes\Wesnoth-like\theme.cfg"
  Delete "$INSTDIR\themes\Wesnoth-like\sea.png"
  Delete "$INSTDIR\themes\Wesnoth-like\pasture.png"
  Delete "$INSTDIR\themes\Wesnoth-like\mountain.png"
  Delete "$INSTDIR\themes\Wesnoth-like\hill.png"
  Delete "$INSTDIR\themes\Wesnoth-like\gold.png"
  Delete "$INSTDIR\themes\Wesnoth-like\forest.png"
  Delete "$INSTDIR\themes\Wesnoth-like\field.png"
  Delete "$INSTDIR\themes\Wesnoth-like\desert.png"
  Delete "$INSTDIR\themes\Wesnoth-like\board.png"
  Delete "$INSTDIR\themes\Tiny\wool-port.png"
  Delete "$INSTDIR\themes\Tiny\wool-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\theme.cfg"
  Delete "$INSTDIR\themes\Tiny\sea-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\ore-port.png"
  Delete "$INSTDIR\themes\Tiny\ore-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\lumber-port.png"
  Delete "$INSTDIR\themes\Tiny\lumber-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\grain-port.png"
  Delete "$INSTDIR\themes\Tiny\grain-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\gold-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\desert-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\brick-port.png"
  Delete "$INSTDIR\themes\Tiny\brick-lorindol.png"
  Delete "$INSTDIR\themes\Tiny\board.png"
  Delete "$INSTDIR\themes\Iceland\theme.cfg"
  Delete "$INSTDIR\themes\Iceland\pasture_wool.png"
  Delete "$INSTDIR\themes\Iceland\mountain_ore.png"
  Delete "$INSTDIR\themes\Iceland\hill_brick.png"
  Delete "$INSTDIR\themes\Iceland\gold.png"
  Delete "$INSTDIR\themes\Iceland\forest_lumber.png"
  Delete "$INSTDIR\themes\Iceland\field_grain.png"
  Delete "$INSTDIR\themes\Iceland\desert.png"
  Delete "$INSTDIR\themes\FreeCIV-like\theme.cfg"
  Delete "$INSTDIR\themes\FreeCIV-like\sea.png"
  Delete "$INSTDIR\themes\FreeCIV-like\pasture.png"
  Delete "$INSTDIR\themes\FreeCIV-like\mountain.png"
  Delete "$INSTDIR\themes\FreeCIV-like\hill.png"
  Delete "$INSTDIR\themes\FreeCIV-like\forest.png"
  Delete "$INSTDIR\themes\FreeCIV-like\field.png"
  Delete "$INSTDIR\themes\FreeCIV-like\desert.png"
  Delete "$INSTDIR\themes\FreeCIV-like\board.png"
  Delete "$INSTDIR\pixmaps\pioneers\trade.png"
  Delete "$INSTDIR\pixmaps\pioneers\splash.png"
  Delete "$INSTDIR\pixmaps\pioneers\ship_move.png"
  Delete "$INSTDIR\pixmaps\pioneers\ship.png"
  Delete "$INSTDIR\pixmaps\pioneers\settlement.png"
  Delete "$INSTDIR\pixmaps\pioneers\road.png"
  Delete "$INSTDIR\pixmaps\pioneers\finish.png"
  Delete "$INSTDIR\pixmaps\pioneers\dice.png"
  Delete "$INSTDIR\pixmaps\pioneers\develop.png"
  Delete "$INSTDIR\pixmaps\pioneers\city.png"
  Delete "$INSTDIR\pixmaps\pioneers\bridge.png"
;  Delete "$INSTDIR\man\man6\pioneersai.6"
;  Delete "$INSTDIR\man\man6\pioneers.6"
;  Delete "$INSTDIR\man\man6\pioneers-server-gtk.6"
;  Delete "$INSTDIR\man\man6\pioneers-server-console.6"
;  Delete "$INSTDIR\man\man6\pioneers-meta-server.6"
  Delete "$INSTDIR\locale\nl\LC_MESSAGES\pioneers.mo"
  Delete "$INSTDIR\locale\it\LC_MESSAGES\pioneers.mo"
  Delete "$INSTDIR\locale\hu\LC_MESSAGES\pioneers.mo"
  Delete "$INSTDIR\locale\fr\LC_MESSAGES\pioneers.mo"
  Delete "$INSTDIR\locale\es\LC_MESSAGES\pioneers.mo"
  Delete "$INSTDIR\locale\de\LC_MESSAGES\pioneers.mo"
  Delete "$INSTDIR\games\pioneers\x.game"
  Delete "$INSTDIR\games\pioneers\star.game"
  Delete "$INSTDIR\games\pioneers\square.game"
  Delete "$INSTDIR\games\pioneers\small.game"
  Delete "$INSTDIR\games\pioneers\seafarers.game"
  Delete "$INSTDIR\games\pioneers\seafarers-gold.game"
  Delete "$INSTDIR\games\pioneers\pond.game"
  Delete "$INSTDIR\games\pioneers\Mini_another_swimming_pool_in_the_wall.game"
  Delete "$INSTDIR\games\pioneers\lorindol.game"
  Delete "$INSTDIR\games\pioneers\iles.game"
  Delete "$INSTDIR\games\pioneers\henjes.game"
  Delete "$INSTDIR\games\pioneers\GuerreDe100ans.game"
  Delete "$INSTDIR\games\pioneers\four-islands.game"
  Delete "$INSTDIR\games\pioneers\Evil_square.game"
  Delete "$INSTDIR\games\pioneers\default.game"
  Delete "$INSTDIR\games\pioneers\Cube.game"
  Delete "$INSTDIR\games\pioneers\crane_island.game"
  Delete "$INSTDIR\games\pioneers\conquest.game"
  Delete "$INSTDIR\games\pioneers\conquest+ports.game"
  Delete "$INSTDIR\games\pioneers\computer_names"
  Delete "$INSTDIR\games\pioneers\coeur.game"
  Delete "$INSTDIR\games\pioneers\canyon.game"
  Delete "$INSTDIR\games\pioneers\archipel_gold.game"
  Delete "$INSTDIR\games\pioneers\Another_swimming_pool_in_the_wall.game"
  Delete "$INSTDIR\games\pioneers\5-6-player.game"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Website.lnk"
  Delete "$DESKTOP\Pioneers.lnk"
  Delete "$DESKTOP\Pioneers Editor.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Pioneers.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Pioneers Editor.lnk"

  RMDir "$SMPROGRAMS\$ICONS_GROUP"
  RMDir "$INSTDIR\themes\Wesnoth-like"
  RMDir "$INSTDIR\themes\Tiny"
  RMDir "$INSTDIR\themes\Iceland"
  RMDir "$INSTDIR\themes\FreeCIV-like"
  RMDir "$INSTDIR\themes"
  RMDir "$INSTDIR\pixmaps\pioneers"
  RMDir "$INSTDIR\pixmaps"
  RMDir "$INSTDIR\locale\nl\LC_MESSAGES"
  RMDir "$INSTDIR\locale\nl"
  RMDir "$INSTDIR\locale\it\LC_MESSAGES"
  RMDir "$INSTDIR\locale\it"
  RMDir "$INSTDIR\locale\hu\LC_MESSAGES"
  RMDir "$INSTDIR\locale\hu"
  RMDir "$INSTDIR\locale\fr\LC_MESSAGES"
  RMDir "$INSTDIR\locale\fr"
  RMDir "$INSTDIR\locale\es\LC_MESSAGES"
  RMDir "$INSTDIR\locale\es"
  RMDir "$INSTDIR\locale\de\LC_MESSAGES"
  RMDir "$INSTDIR\locale\de"
  RMDir "$INSTDIR\locale"
  RMDir "$INSTDIR\games\pioneers"
  RMDir "$INSTDIR\games"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd


; CheckGtkVersion
; inputs: Push 2 GTK+ version strings to check. The major value needs to
; be equal and the minor value needs to be greater or equal.
;
; Usage:
;   Push "2.1.0"  ; Reference version
;   Push "2.2.1"  ; Version to check
;   Call CheckGtkVersion
;   Pop $R0
;   $R0 will now equal "1", because 2.2 is greater than 2.1
;
Function CheckGtkVersion
  ; Version we want to check
  Exch $R0
  Exch
  ; Reference version
  Exch $R1
  Push $R2
  Push $R3

  ; Check that the string to check is at least 5 chars long (i.e. x.x.x)
  StrLen $R2 $R0
  IntCmp $R2 5 0 bad_version

  ; Major version check
  StrCpy $R2 $R0 1
  StrCpy $R3 $R1 1
  IntCmp $R2 $R3 check_minor bad_version bad_version

  check_minor:
    StrCpy $R2 $R0 1 2
    StrCpy $R3 $R1 1 2
    IntCmp $R2 $R3 good_version bad_version good_version

  bad_version:
    StrCpy $R0 "0"
    Goto done

  good_version:
    StrCpy $R0 "1"

  done:
    Pop $R3
    Pop $R2
    Pop $R1
    Exch $R0
FunctionEnd

;
; Usage:
; Call DoWeNeedGtk
; First Pop:
;   0 - We have the correct version
;       Second Pop: Key where Version was found
;   1 - We have an old version that needs to be upgraded
;       Second Pop: HKLM or HKCU depending on where GTK was found.
;   2 - We don't have Gtk+ at all
;       Second Pop: "NONE, HKLM or HKCU" depending on our rights..
;
Function DoWeNeedGtk
  ; Logic should be:
  ; - Check what user rights we have (HKLM or HKCU)
  ;   - If HKLM rights..
  ;     - Only check HKLM key for GTK+
  ;       - If installed to HKLM, check it and return.
  ;   - If HKCU rights..
  ;     - First check HKCU key for GTK+
  ;       - if good or bad exists stop and ret.
  ;     - If no hkcu gtk+ install, check HKLM
  ;       - If HKLM ver exists but old, return as if no ver exits.
  ;   - If no rights
  ;     - Check HKLM
  Push $0
  Push $2
  Push $3
  Push $4
  Push $5

  Call CheckUserInstallRights
  Pop $3
  StrCmp $3 "HKLM" check_hklm
  StrCmp $3 "HKCU" check_hkcu check_hklm
    check_hkcu:
      ReadRegStr $0 HKCU ${GTK_REG_KEY} "Version"
      StrCpy $5 "HKCU"
      StrCmp $0 "" check_hklm have_gtk

    check_hklm:
      ReadRegStr $0 HKLM ${GTK_REG_KEY} "Version"
      StrCpy $5 "HKLM"
      StrCmp $0 "" no_gtk have_gtk


  have_gtk:
    ; GTK+ is already installed.. check version.
    Push ${GTK_VERSION} ; Minimum GTK+ version needed
    Push $0
    Call CheckGtkVersion
    Pop $2
    StrCmp $2 "1" good_version bad_version
    bad_version:
      ; Bad version. If hklm ver and we have hkcu or no rights.. return no gtk
      StrCmp $3 "NONE" no_gtk  ; if no rights.. can't upgrade
      StrCmp $3 "HKCU" 0 upgrade_gtk ; if HKLM can upgrade..
        StrCmp $5 "HKLM" no_gtk upgrade_gtk ; have hkcu rights.. if found hklm ver can't upgrade..

      upgrade_gtk:
        StrCpy $2 "1"
        Push $5
        Push $2
        Goto done

  good_version:
    StrCmp $5 "HKLM" have_hklm_gtk have_hkcu_gtk
      have_hkcu_gtk:
        ; Have HKCU version
        ReadRegStr $4 HKCU ${GTK_REG_KEY} "Path"
        Goto good_version_cont

      have_hklm_gtk:
        ReadRegStr $4 HKLM ${GTK_REG_KEY} "Path"
        Goto good_version_cont

    good_version_cont:
      StrCpy $2 "0"
      Push $4  ; The path to existing GTK+
      Push $2
      Goto done

  no_gtk:
    StrCpy $2 "2"
    Push $3 ; our rights
    Push $2
    Goto done

  done:
  ; The top two items on the stack are what we want to return
  Exch 5
  Pop $0
  Exch 5
  Pop $2
  Pop $5
  Pop $4
  Pop $3
FunctionEnd

Function preWelcomePage
  ; If this installer doesn't have GTK, check whether we need it.
  ; We do this here an not in .onInit because language change in
  ; .onInit doesn't take effect until it is finished.
    Push $R0
    Call DoWeNeedGtk
    Pop $R0
    Pop $GTK_FOLDER

    StrCmp $R0 "0" have_gtk need_gtk
    need_gtk:
      IfSilent skip_mb
        MessageBox MB_ICONSTOP|MB_OK "${GTK_VERSION_TEXT}"
      skip_mb:
      Quit
    have_gtk:
    Pop $R0
FunctionEnd

Function CheckUserInstallRights
  Push $0
  Push $1
  ClearErrors
  UserInfo::GetName
  IfErrors Win9x
  Pop $0
  UserInfo::GetAccountType
  Pop $1

  StrCmp $1 "Admin" 0 +3
    StrCpy $1 "HKLM"
    Goto done
  StrCmp $1 "Power" 0 +3
    StrCpy $1 "HKLM"
    Goto done
  StrCmp $1 "User" 0 +3
    StrCpy $1 "HKCU"
    Goto done
  StrCmp $1 "Guest" 0 +3
    StrCpy $1 "NONE"
    Goto done
  ; Unknown error
  StrCpy $1 "NONE"
  Goto done

  Win9x:
    StrCpy $1 "HKLM"

  done:
    Exch $1
    Exch
    Pop $0
FunctionEnd
